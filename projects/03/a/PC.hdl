// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // load the register if either reset/load/inc
    Or(a=reset, b=load, out=intload);
    Or(a=intload, b=inc, out=regload);
    
    // Use load input bit to select between increment and input
    Inc16(in=regout, out=incval);
    Mux16(a=incval, b=in, sel=load, out=muxout);

    // Use reset bit to select between previous selection and zeros
    Mux16(a=muxout, b[0..15]=false, sel=reset, out=regin);

    Register(in=regin, load=regload, out=regout, out=out);
}
